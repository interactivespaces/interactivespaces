/*
 * Copyright (C) 2013 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the * License for the specific language governing permissions and limitations under
 * the License.
 */

dependencies {
  compile project(':interactivespaces-core')
  compile project(':interactivespaces-system')

  compile files("${project.getProperty('opencv.home')}/bin/opencv-247.jar")

  provided 'org.apache.felix:org.apache.felix.framework:3.2.2'

  testCompile 'junit:junit:4.8.2'
  testCompile 'org.mockito:mockito-all:1.9.5'
}

task copyResources(type: Copy) {
  from 'src/main/resources/extras'
  into 'build/staging/controller/extras'
}

task copyOpenCVJars(type: Copy) {
  from "${project.getProperty('opencv.home')}/bin"
  into 'build/staging/controller/extras/opencv/jars'
  include '**/*.jar'
}

task copyOpenCVNativeLibs(type: Copy) {
  from "${project.getProperty('opencv.home')}/lib"
  into 'build/staging/controller/extras/opencv/native/linux'
  exclude '**/*.a'
}

task copyExtraComponents(dependsOn: [copyResources, copyOpenCVNativeLibs, copyOpenCVJars])

install.dependsOn.add copyExtraComponents

jar {
  manifest {
    version = osgiVersion
    symbolicName = 'interactivespaces.service.image.vision'

    instruction 'Bundle-Activator',
        'interactivespaces.service.image.vision.internal.osgi.ImageVisionServiceActivator'

    instruction 'Export-Package',
        '!interactivespaces.service.image.vision.internal.*',
        '*'

    instruction 'Private-Package',
        'interactivespaces.service.image.vision.internal.*'
  }
}
