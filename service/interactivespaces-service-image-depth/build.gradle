/*
 * Copyright (C) 2013 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the * License for the specific language governing permissions and limitations under
 * the License.
 */

ext {
  serviceFolderName = "interactivespaces.service.image.depth"
  baseServiceBuildFolder = "${project.ext.extrasBuildFolder}/${project.ext.serviceFolderName}"
}

dependencies {
  compile project(':interactivespaces-core')
  compile project(':interactivespaces-system')
  compile project(':interactivespaces-interaction')
  compile project(':service:interactivespaces-service-image-depth-openni2')

  compile('com.nativelibs4java:bridj:0.7.0') { exclude module: 'dx'  }

  provided 'org.apache.felix:org.apache.felix.framework:5.0.0'

  testCompile 'junit:junit:4.8.2'
  testCompile 'org.mockito:mockito-all:1.9.5'
}

task copyResources(type: Copy) {
  from 'src/main/resources/extras'
  into "${project.ext.baseServiceBuildFolder}"
}

task copyExtrasNativeLibs1(type: Copy) {
  from "${project.getProperty('interactivespaces.dependencies.home')}/openni/2.2/native"
  into "${project.ext.baseServiceBuildFolder}/openni/native"
  exclude '**/*.a'
}

task copyExtrasNativeLibs2(type: Copy) {
  from "${project.getProperty('interactivespaces.dependencies.home')}/nite/2.2/native"
  into "${project.ext.baseServiceBuildFolder}/nite/native"
  exclude '**/*.a'
}

task copyExtraComponents(dependsOn: [
  copyResources,
  copyExtrasNativeLibs1,
  copyExtrasNativeLibs2
])

task copyServiceArtifacts(type: Copy, dependsOn: [install]) {
  from configurations.archives.allArtifacts.files
  into "${project.ext.baseServiceBuildFolder}"
}

task assembleExtras(dependsOn: [copyExtraComponents, copyServiceArtifacts])

jar {
  manifest {
    version = osgiVersion
    symbolicName = 'interactivespaces.service.image.depth'

    instruction 'Bundle-Activator',
        'interactivespaces.service.image.depth.internal.osgi.DepthCameraServiceActivator'

    instruction 'Export-Package',
        '!interactivespaces.service.image.depth.internal.*',
        '*'

    instruction 'Private-Package',
        'interactivespaces.service.image.depth.internal.*'

    instruction 'Import-Package',
        'org.bridj.*;version="0.0.0"',
        'interactivespaces.service.image.depth.internal.openni2.libraries;version="0.0.0"',
        '*'
  }
}
